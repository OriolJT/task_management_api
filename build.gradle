plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.orioljt'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    byteBuddyAgent
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.10'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-database-postgresql:11.11.1'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    byteBuddyAgent 'net.bytebuddy:byte-buddy-agent:1.17.7'
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    doFirst {
        def agentFile = configurations.byteBuddyAgent.singleFile
        jvmArgs "-javaagent:${agentFile.absolutePath}"
    }
    testLogging {
        events 'FAILED', 'SKIPPED'
        exceptionFormat 'short'
        showExceptions true
        showCauses true
        showStackTraces false
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}
